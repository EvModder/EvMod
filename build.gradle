plugins {
	id 'fabric-loom'
	id 'maven-publish'
}

//loom { accessWidenerPath = file("src/main/resources/accesswidener") }

base {
	group = project.maven_group
	version = project.mod_version
	archivesName = project.mod_name + '-' + project.minecraft_version
}

// These get passed to src/main/java/resources/fabric.mod.json
processResources {
	filesMatching("fabric.mod.json") {
		def valueMap = [
			"mod_id"			: project.mod_id,
			"mod_name"			: project.mod_name,
			"mod_version"		: project.mod_version,
			"loader_version"	: project.loader_version,
			"minecraft_version" : project.minecraft_version,
			"malilib_version"	: project.malilib_version
		]
		expand valueMap
	}
}

repositories {
	// https://docs.gradle.org/current/userguide/declaring_repositories.html
	// You should only use this when depending on other mods.
	// Loom adds the essential maven repositories to download Minecraft and libraries from automatically.

	maven { url = 'https://jitpack.io' } // New home of MaLiLib
//	maven { url = "https://masa.dy.fi/maven" }
//	maven { url 'https://www.cursemaven.com' }
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"

	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// Fabric API. Technically optional.
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_api_version}"

//	include(implementation(annotationProcessor("io.github.llamalad7:mixinextras-fabric:${project.mixinextras_version}")))

//	modImplementation "fi.dy.masa.malilib:${project.minecraft_version}-${project.malilib_version}"
	modImplementation "com.github.sakura-ryoko:malilib:${project.minecraft_version}-${project.malilib_version}"
}

// https://github.com/EvModder/EvLib
sourceSets { main { java { srcDirs 'src/main/java', '../../Plugins/EvLib/src/net/evmodder/EvLib/util/' } } }

compileJava.options.encoding = 'UTF-8'

tasks.withType(JavaCompile).configureEach { it.options.release = 21 }

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and add to the "build" task.
	// If you remove this line, sources will not be generated.
//	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
}

jar {
	inputs.property "archivesName", project.base.archivesName

	from("LICENSE") { rename { "${it}_${inputs.properties.archivesName}"} }
}


publishing {
	// https://docs.gradle.org/current/userguide/publishing_maven.html

	publications {
		create("mavenJava", MavenPublication) {
			artifactId = project.base.archivesName
			from components.java
		}
	}
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// Repositories here are used for publishing your artifact, not retrieving dependencies.
	}
}